plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.ossLicenses)
    alias(libs.plugins.googleServices)
    alias(libs.plugins.firebaseCrashlytics)
    alias(libs.plugins.hiltAndroid)
    id 'jacoco' // ADDED: JaCoCo Plugin
}

android {
    compileSdkVersion 36
    namespace 'com.d4rk.androidtutorials.java'
    defaultConfig {
        applicationId 'com.d4rk.androidtutorials.java'
        minSdk 23
        targetSdk 36
        versionCode 58
        versionName '5.0.5'
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        resourceConfigurations += [
                'ar-rEG',
                'bg-rBG',
                'bn-rBD',
                'de-rDE',
                'en',
                'es-rGQ',
                'es-rMX',
                'fil-rPH',
                'fr-rFR',
                'hi-rIN',
                'hu-rHU',
                'in-rID',
                'it-rIT',
                'ja-rJP',
                'ko-rKR',
                'pl-rPL',
                'pt-rBR',
                'ro-rRO',
                'ru-rRU',
                'sv-rSE',
                'th-rTH',
                'tr-rTR',
                'uk-rUA',
                'ur-rPK',
                'vi-rVN',
                'zh-rTW'
        ]
    }

    buildTypes {
        release {
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            multiDexEnabled true
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
        coreLibraryDesugaringEnabled true
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    bundle {
        storeArchive {
            enable true
        }
    }
}

//Added: JaCoCo Tool Version Configuration

jacoco {
    toolVersion = "0.8.10"
}

// ADDED: Task to generate the HTML Report
task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for the Debug build"

    reports {
        xml.required = true
        html.required = true
    }

    // Standard filter for auto-generated files that shouldn't be included in coverage
    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Binding.class', '**/databinding/*', '**/di/*', '**/utils/*']

    // Path to the compiled Java and Kotlin classes for the debug build
    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/classes", excludes: fileFilter)
    
    // NOTE: If you are using Kotlin alongside Java, you might need to adjust the path if Kotlin classes are compiled separately.
    // For a Java-only project, this should suffice.
    
    classDirectories.from = files([debugTree])

    // Source files for displaying code in the HTML report
    sourceDirectories.from = files([
        "${project.projectDir}/src/main/java"
    ])

    // Execution data generated when unit tests are run
    executionData.from = files("${buildDir}/outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec")

    // The execution data path may vary slightly depending on your Gradle version.
    // If the path above fails, try: "${buildDir}/jacoco/testDebugUnitTest.exec"
}



dependencies {

    coreLibraryDesugaring libs.desugar.jdk.libs

    // Google Play services and related UI components
    implementation libs.bundles.google.core

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.bundles.firebase.services

    // Jetpack libraries
    implementation libs.bundles.androidx.core
    implementation libs.bundles.androidx.lifecycle

    // Shared UI tooling and visuals
    implementation libs.bundles.ui.toolkit

    // Networking and serialization
    implementation libs.bundles.networking

    // Dependency injection
    implementation libs.hilt.android

    // Annotation processors
    annotationProcessor libs.hilt.compiler
    annotationProcessor libs.androidx.room.compiler
    annotationProcessor libs.glide.compiler

    // Testing
    testImplementation libs.bundles.unitTest
}
